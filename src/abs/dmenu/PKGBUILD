# $Id: PKGBUILD 83924 2010-06-23 18:58:07Z andrea $
# Maintainer:
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=dmenu-mod
pkgver=4.1.1
pkgrel=1
pkgdesc="A generic menu for X"
url="http://tools.suckless.org/dmenu/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama')
provides=('dmenu=4.1.1')
conflicts=('dmenu')
source=("http://dl.suckless.org/tools/dmenu-$pkgver.tar.gz"
        tab-space.patch
        textmargin.patch)
md5sums=('931896a199741c23be945e8519a81c1d'
         '409b61f6e6c913cbb356e1ac03ba8a24'
         '2dd9ea4a8055d5be8732154493e1d2e2')

build(){
  cd $srcdir/dmenu-$pkgver

  patch -Np1 < ../tab-space.patch || return 1
  patch -Np1 < ../textmargin.patch || return 1
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/dmenu-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install

  #install license
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
