# Maintainer: Devin J. Pohly <djpohly@gmail.com>
pkgname=dmenu-djp-git
pkgver=494.5
pkgrel=1
pkgdesc="The latest git pull of dmenu, with my own patchqueue"
arch=(i686 x86_64)
url="http://tools.suckless.org/dmenu/"
license=('MIT')
depends=('sh' 'libxinerama')
makedepends=('git')
provides=('dmenu')
conflicts=('dmenu')
source=("git+https://github.com/djpohly/dmenu#branch=patches")
_upstream="http://git.suckless.org/dmenu"
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/dmenu"

  git fetch "$_upstream" master

  local upstream=$(git rev-list --count FETCH_HEAD)
  local behind=$(git rev-list --count ..FETCH_HEAD)
  local ahead=$(git rev-list --count FETCH_HEAD..)

  [[ $behind -gt 0 ]] && warning "Patched version is %s commits behind upstream" "$behind"

  printf "%s.%s\n" "$upstream" "$ahead"
}

build() {
  cd "$srcdir/dmenu"

  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/dmenu"

  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
